this is how i set up my iac code for this project

as the iac tool stack i chose opentofu + terragrunt.

first i needed provision 2 s3 buckets for storing statefiles in both dev and prod accounts.

i did it manually, because terragrunt was not able to handle them for me. Even tough it should, based on what the [docs](https://terragrunt.gruntwork.io/docs/features/state-backend/#:~:text=When%20you,enabled) say

tree looks like this, and this structure was chosen based on best practice provided by gruntworks in their [terragrunt guide](https://terragrunt.gruntwork.io/docs/features/state-backend/#:~:text=When%20you,enabled)

it has small tweaks, like env_vars and common_vars, since i wanted to have greater control over the variables passed to units and wanted to avoid as many duplications as possible.

```
> tree
.
├── catalog
│   └── modules
│       └── ddb
│           ├── main.tf
│           ├── outputs.tf
│           └── variables.tf
└── live
    ├── common_vars.hcl
    ├── dev
    │   ├── ddb
    │   │   └── terragrunt.hcl
    │   └── env_vars.hcl
    ├── prod
    │   ├── ddb
    │   │   └── terragrunt.hcl
    │   └── env_vars.hcl
    └── root.hcl
```

terragunt.hcl file inside each unit contains minimal information

```
# module source code
terraform {
  source = "${find_in_parent_folders("catalog/modules")}//ddb"
}

# expose allows to use locals block from this file
include "root" {
  path   = find_in_parent_folders("root.hcl")
  expose = true
}

inputs = {
  name     = include.root.locals.common_vars.locals.name
  env_type = include.root.locals.env_vars.locals.env_type
}
```
i am not sure if `expose = true` should be considered a bad practice. probably it would be better to directly load `env_vars` and `common_vars` in terragrunt.hcl but they are already being imported in root.hcl file and such setup allows me to add more files and import them once in root.hcl and use across all units without changing terragrunt.hcl for each one of them. so that works perfectly for me now.

then there are env vars:

```hcl
# Set common variables for the environment. This is automatically pulled in in the root root.hcl configuration to
# configure the remote state bucket and is accessible as inputs in child units.
locals {
  env_type = "dev"

  env_tags = {
    Environment = local.env_type
  }
}

```

and common vars:
```hcl
locals {
    name = "jubio"

    aws_region = "eu-central-1"

    common_tags = {
        App                 = local.name
        Owner               = "Ziemi" 
        ManagedByTerragrunt = "True"
    }
}
```

i decided to not create region vars since for now i only plan to deploy to one region.
however multiple new regions could be easily added by manipulating the variables.

simple example would be to create additional directory `region/` inside environments and move units inside
region directory, then in this directory we'd create regions.hcl file and place there for example aws_region. boom. now we can deploy this config to multiple regions!

and i wanted to have one root.hcl file for all environments to be able to automate providers.tf and backend.tf generation from one place.

```hcl
generate "providers" {
  path      = "providers.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
provider "aws" {
  region = "${local.aws_region}"

  default_tags {
    tags = ${jsonencode(local.default_tags)}
  }
}
EOF
}


generate "versions" {
  path      = "versions.tf"
  if_exists = "overwrite_terragrunt"

  contents = <<EOF
terraform {
  required_version = "${local.tofu_version}"

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "${local.aws_provider_version}"
    }
  }
}
EOF
}

```

this allows providers and tofu versions across different environments, which is fine for my setup
but probably would not be ideal for setup were different components need to use different providers (like eks cluster setup). and this is because generate block created those files inside different units

for example `infra/live/dev/ddb/.terragrunt-cache/xxxxx/yyyyy/ddb/providers.tf` looks like this:

```hcl
# Generated by Terragrunt. Sig: ZZZZZ
provider "aws" {
  region = "eu-central-1"

  default_tags {
    tags = { "App" : "jubio", "Environment" : "dev", "ManagedByTerragrunt" : "True", "Owner" : "Ziemi" }
  }
}
```

this setup seems very scalable since if I want to add new environment only thing i need to do is to copy-paste one of currently existing environments, change it name and adjust `env_vars`. and boom. its ready to be deployed

